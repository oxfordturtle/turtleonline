void main () {
  blank(cream);
  // print instructions
  setxy(15, 200);
  display('This reaction game will display a sequence', 4, 36);
  setxy(20, 270);
  display('of 10 coloured circles on the Canvas.  Try', 4, 36);
  setxy(20, 340);
  display('to click the mouse on each circle as quickly', 4, 36);
  setxy(20, 410);
  display('as you can, and see how short a total time', 4, 36);
  setxy(20, 480);
  display('you can achieve for all 10.', 4, 36);
  setxy(100, 620);
  display('Now press the "Esc" key to start ...', 4, 36);
  keyecho(false);
  // wait for escape key before proceeding...
  do {} while (abs(?key) != \escape);
  // reset timer
  timeset(0);
  for (int count = 1; count <= 10; count = count + 1) {
    // blank the screen
    blank(black);
    // draw a blot at random coordinates
    setxy(rand(900) + 50, rand(900) + 50);
    colour(rgb(count));
    blot(50);
    // until the blot has been clicked...
    do {
      // wait for left mouse click
      do {} while (?lmouse <= 0);
      // record the colour at the cursor's coordinates
      int clickColour = pixcol(?mousex, ?mousey);
    } while (clickColour != turtc);
  }
  // print time taken
  blank(lightblue);
  setxy(60, 460);
  colour(lightred);
  display('Your time was ' + qitoa(time(), 1000, 2) + ' seconds', 4, 50);
}
